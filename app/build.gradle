import org.eclipse.jgit.api.Git

plugins {
  id 'com.android.application'
  id 'kotlin-android'
}

android {
  compileSdkVersion 30

  defaultConfig {
    applicationId "repro.sample"
    minSdkVersion 21
    targetSdkVersion 30
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

// Helper task to generate and init a local git repository which is referenced as a source dependency
tasks.register("generateRepoFolder") { task ->

  task.outputs.dir("$rootDir/repo-ui-tests")

  task.doLast("run") { runtimeTask ->

    def outDir = runtimeTask.outputs.files.singleFile
    def project = runtimeTask.project

    project.delete(new File(outDir, ".git"))

    Git.init().setDirectory(outDir).call().withCloseable { git ->

      new File(outDir, "should-be-included-in-assets.txt")
        .write("Hello\nWorld!")

      git.add().addFilepattern("should-be-included-in-assets.txt").call()
      git.commit().setMessage("initial commit").call()
    }
  }
}

tasks.named("preBuild") {

  it.dependsOn(tasks.named("generateRepoFolder"))
}

dependencies {
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'com.google.android.material:material:1.2.1'

  testImplementation 'junit:junit:4.13.1'

  androidTestImplementation 'androidx.test.ext:junit:1.1.2'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

  // AS import/sync fails while trying to "model" this source dependency
  // uncomment the block comments to remove this source dependency and make import/sync pass

  ///*
  androidTestImplementation('repro.sample.internal:ui-tests') {
    version { branch = "master" }
  }
  //*/
}